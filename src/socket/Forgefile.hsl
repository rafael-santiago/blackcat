#
#                          Copyright (C) 2018 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#
#

include ../forgeutils/toolsets.hsl

local var src type list;
local var inc type list;
local var cflags type list;
local var lib type list;
local var ldflags type list;
local var deps type string;
local var curr_tool type string;

libbcsck.preloading() {
    $curr_tool = get_lib_toolset();
}

project libbcsck : toolset $curr_tool : dependencies $deps : $src, $inc, $cflags, $lib, $ldflags, "libbcsck.so";

libbcsck.prologue() {
    if (build("libkryptos") != 0 || build("libcutest") != 0) {
        hefesto.project.abort(1);
    }

    $inc = hefesto.sys.get_option("includes");
    $cflags = hefesto.sys.get_option("cflags");
    $lib = hefesto.sys.get_option("libraries");
    $ldflags = hefesto.sys.get_option("ldflags");

    var option type list;
    $option = hefesto.sys.get_option("no-thread-safe");

    if ($option.count() > 0) {
        $cflags.del_item("-DBCSCK_THREAD_SAFE=1");
    }

    if (hefesto.sys.os_name() == "netbsd") {
        $cflags.add_item("-std=c99");
    } else if (hefesto.sys.os_name() == "linux") {
        $ldflags.add_item("-ldl");
        if ($cflags.index_of("-DBCSCK_THREAD_SAFE=1") > -1) {
            $ldflags.add_item("-pthread");
        }
    } else if (hefesto.sys.os_name() == "minix") {
        $cflags.del_item("-DBCSCK_THREAD_SAFE=1");
    }

    var dirlist type list;

    $dirlist.add_item(hefesto.sys.pwd());

    $src = get_codes($dirlist, $deps);
}
