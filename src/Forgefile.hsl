#
#                          Copyright (C) 2018 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#
#

include ~/toolsets/null/null.hsl
include ~/toolsets/utils/forge_projects.hsl
include forgeutils/installer.hsl
include forgeutils/toolsets.hsl

var exit_code type int;

project blackcat : toolset "no-tool-any-set" : $exit_code;

blackcat.prologue() {
    var option type list;

    $option = hefesto.sys.get_option("avail-ciphers");

    if ($option.count() > 0) {
        var dummy type list;
        setup_ciphering_schemes($dummy);
    }

    $option = hefesto.sys.get_option("install");

    if ($option.count() == 0) {
        $option = hefesto.sys.get_option("uninstall");
    }

    if ($option.count() > 0) {
        installer();
    }

    # INFO(Rafael): This is useful in order to find artifacts from some submodules.
    hefesto.sys.setenv("BLACKCAT_ROOTDIR", hefesto.sys.pwd());

    $option = hefesto.sys.get_option("forge-anyway");

    if ($option.count() > 0) {
        hefesto.sys.rm("lib/kryptos/lib/libkryptos.a");
        hefesto.sys.rm("lib/cutest/src/lib/libcutest.a");
        hefesto.sys.rm("lib/accacia/lib/libaccacia.a");
    }

    var subforges type list;
    $subforges = hefesto.sys.get_option("modules");

    if ($subforges.count() == 0) {
        $subforges.add_item("base");
        $subforges.add_item("fs");
        if (hefesto.sys.os_name() != "windows") {
            $subforges.add_item("net");
            $subforges.add_item("socket");
        }

        $option = hefesto.sys.get_option("no-dev");

        if ($option.count() == 0) {
            var blackcat_dev_supp type list;
            $blackcat_dev_supp = hefesto.sys.lines_from_file("BCDEV_PLATFORMS", ".*");
            if ($blackcat_dev_supp.index_of(hefesto.sys.os_name()) > -1) {
                $subforges.add_item("dev");
            }
        }

        $subforges.add_item("cmd");
    }

    $option = hefesto.sys.get_option("clean-modules");
    if ($option.count() > 0) {
        mkclean_modules($subforges);
    }

    $exit_code = forge_projects($subforges);
}

blackcat.epilogue() {
    if ($exit_code == 0) {
        hefesto.sys.echo("INFO: Build success.\n");
    } else {
        hefesto.sys.echo("ERROR: Build has failed.\n");
    }
}
