#
#                          Copyright (C) 2018 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#
#

include ../forgeutils/toolsets.hsl

local var includes type list;
local var cflags type list;
local var libraries type list;
local var ldflags type list;

local var toolset_name type string;

local var reverse_pattern type string;

# INFO(Rafael): The device will be tested in cmd's system tests only. Since this is pretty straightforward and once
#               within the kernel it will stay resident until the next system's reboot, let's avoid complicating the
#               whole build task including weird reboot steps.

project blackcat-dev : toolset $toolset_name : "cdev.c", $includes, $cflags, $libraries, $ldflags, "blackcat";

blackcat-dev.preloading() {
    $toolset_name = get_lkm_toolset();
}

blackcat-dev.prologue() {
    var option type list;

    $option = hefesto.sys.get_option("clean");

    if ($option.count() > 0) {
        mkclean();
    }

    $includes.add_item(hefesto.sys.pwd());
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), "defs"));
    $includes.add_item(hefesto.sys.make_path(hefesto.sys.pwd(), hefesto.sys.os_name()));

    var rootpath type string;

    $rootpath = hefesto.sys.make_path(hefesto.sys.pwd(), "icloak/src");

    $includes.add_item($rootpath);
    $includes.add_item(hefesto.sys.make_path($rootpath, "utils"));
    $includes.add_item(hefesto.sys.make_path($rootpath, hefesto.sys.os_name()));

    $rootpath = hefesto.sys.make_path($rootpath, "kook/src");

    $includes.add_item($rootpath);
    $includes.add_item(hefesto.sys.make_path($rootpath, "utils"));
    $includes.add_item(hefesto.sys.make_path($rootpath, hefesto.sys.os_name()));

    if (hefesto.sys.os_name() == "netbsd") {
        $option = hefesto.sys.get_option("dev-major");

        var dev_major type string;

        $dev_major = "210";

        if ($option.count() > 0) {
            $dev_major = $option.item(0);
        }

        hefesto.sys.rm("/dev/blackcat");

        var exit_code type int;

        $exit_code = hefesto.sys.run("mknod /dev/blackcat c " + $dev_major + " 0");

        if ($exit_code != 0) {
            hefesto.sys.echo("ERROR: Unable to create the device node.\n");
            hefesto.project.abort($exit_code);
        }

        $reverse_pattern = ", cmajor = " + $dev_major;

        hefesto.sys.replace_in_file("netbsd/cdev_init.c", "CDEV_MAJOR_NR", $dev_major);
    }
}

blackcat-dev.epilogue() {
    if (hefesto.sys.os_name() == "netbsd") {
        hefesto.sys.replace_in_file("netbsd/cdev_init.c", $reverse_pattern, "CDEV_MAJOR_NR");
    }
}
