#
#                          Copyright (C) 2018 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#
#

include ~/fsutil.hsl

function installer() : result type none {
    var option type list;
    var err type int;
    var task type string;

    $option = hefesto.sys.get_option("uninstall");

    if ($option.count() > 0) {
        $task = "uninstall";
    } else {
        $task = "install";
    }

    $err = hefesto.sys.call_func_addr(hefesto.sys.get_func_addr("do_" + $task));

    var s_msg type list;
    $s_msg.add_item("Installed.\n");
    $s_msg.add_item("Uninstalled.\n");

    var e_msg type list;
    $e_msg.add_item("Unable to install.\n");
    $e_msg.add_item("Unable to uninstall.\n");

    var m_idx type int;

    if ($task == "uninstall") {
        $m_idx = 1;
    } else {
        $m_idx = 0;
    }

    if ($err == 0) {
        hefesto.sys.echo("INFO: " + $s_msg.item($m_idx));
    } else {
        hefesto.sys.echo("ERROR: " + $e_msg.item($m_idx));
    }

    hefesto.project.abort($err);
}

local function do_install() : result type int {
    var src type list;
    var dest type list;
    var has_dev type int;

    $has_dev = 0;

    if (hefesto.sys.os_name() == "netbsd" && isfile("dev/blackcat.kmod")) {
        $src.add_item("dev/blackcat.kmod");
        $has_dev = 1;
    } else if (hefesto.sys.os_name() == "linux" || hefesto.sys.os_name() == "freebsd" && isfile("dev/blackcat.ko")) {
        $src.add_item("dev/blackcat.ko");
        $has_dev = 1;
    }

    $src.add_item("../doc/MANUAL.txt");
    if (hefesto.sys.os_name() != "windows") {
        $src.add_item("../etc/man/blackcat.1");
        $src.add_item("../bin/blackcat");
        $src.add_item("lib/libbcsck.so");
    } else {
        $src.add_item("../bin/blackcat.exe");
    }

    if ($has_dev) {
        $dest.add_item("/usr/local/share/blackcat/lkm");
    }

    if (hefesto.sys.os_name() != "windows") {
        $dest.add_item("/usr/local/share/blackcat/doc");
        $dest.add_item("/usr/local/man/man1");
        $dest.add_item("/usr/local/bin");
        if (isdir("/lib")) {
            $dest.add_item("/lib");
        } else {
            $dest.add_item("/usr/local/share/blackcat/lib");
        }
    } else {
        $dest.add_item("C:\\blackcat\\doc");
        $dest.add_item("C:\\blackcat\\bin");
    }

    var f type int;

    $f = 0;

    while ($f < $src.count()) {
        var dpath type string;
        $dpath = $dest.item($f);
        if (isdir($dpath) == 0) {
            mktree($dpath);
        }
        var fname type string;
        $fname = $src.item($f);
        $fname = filenamefrompath($fname);
        $dpath = hefesto.sys.make_path($dest.item($f), $fname);
        if (hefesto.sys.cp($src.item($f), $dpath) != 1) {
            hefesto.sys.echo("ERROR: while creating '" + $dpath + "'\n");
            #do_uninstall();
            result 1;
        }
        $f = $f + 1;
    }

    if (hefesto.sys.os_name() == "windows") {
        var path_data type string;
        $path_data = get_windows_path_env_var_content();
        if ($path_data.match("C:\\\\blackcat\\\\bin") == 0) {
            if ($path_data.at($path_data.len() - 1) != ";") {
                $path_data = $path_data + ";";
            }
            $path_data = $path_data + "C:\\blackcat\\bin";
            if (hefesto.sys.setenv("WINREG:HKLM\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\\PATH",
                                   $path_data) != 0) {
                hefesto.sys.echo("WARN: Unable to export blackcat executable's path.\n" +
                                 "      Do it own your own.\n");

            }
        } else {
            hefesto.sys.echo("WARN: Blackcat executable's path already exported.\n");
        }
    }

    result 0;
}

function do_uninstall() : result type int {
    var src type list;

    if (hefesto.sys.os_name() != "windows") {
        if (isfile("/usr/local/share/blackcat/lib/libbcsck.so")) {
            $src.add_item("/usr/local/share/blackcat/lib/libbcsck.so");
        }

        if (hefesto.sys.os_name() == "netbsd") {
            $src.add_item("/usr/local/share/blackcat/lkm/blackcat.kmod");
        } else if (hefesto.sys.os_name() == "freebsd" || hefesto.sys.os_name() == "linux") {
            $src.add_item("/usr/local/share/blackcat/lkm/blackcat.ko");
        }
        $src.add_item("/usr/local/share/blackcat/doc/MANUAL.txt");

        $src.add_item("/usr/local/share/blackcat");
        $src.add_item("/usr/local/share/blackcat/doc");
        $src.add_item("/usr/local/share/blackcat/lkm");

        if (isdir("/usr/local/share/blackcat/lib")) {
            $src.add_item("/usr/local/share/blackcat/lib");
        }

        $src.add_item("/usr/local/man/man1/blackcat.1");
        $src.add_item("/usr/local/bin/blackcat");
        $src.add_item("/lib/libbcsck.so");
    } else {
        $src.add_item("C:\\blackcat\\doc\\MANUAL.txt");
        $src.add_item("C:\\blackcat\\bin\\blackcat.exe");
        $src.add_item("C:\\blackcat\\doc");
        $src.add_item("C:\\blackcat\\bin");
    }

    var s type int;

    $s = 0;

    while ($s < $src.count()) {
        var path type string;
        $path = $src.item($s);
        if (isdir($path)) {
            rmtree($path);
        } else {
            if (hefesto.sys.rm($path) != 0) {
                hefesto.sys.echo("ERROR: Unable to remove the file '" + $path + "'. Do it on your own.\n");
            }
        }
        $s = $s + 1;
    }

    if (hefesto.sys.os_name() == "netbsd" || hefesto.sys.os_name() == "linux" || hefesto.sys.os_name() == "freebsd") {
        hefesto.sys.echo("INFO: If you have ran the blackcat's LKM you must reboot your system to remove " +
                         "it from your kernel.\n");
    } else if (hefesto.sys.os_name() == "windows") {
        var path_data type string;
        $path_data = get_windows_path_env_var_content();
        if ($path_data.replace(";C:\\\\blackcat\\\\bin", "") > 0) {
            if (hefesto.sys.setenv("WINREG:HKLM\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\\PATH",
                                   $path_data) != 0) {
                hefesto.sys.echo("WARN: Unable to unexport blackcat executable's path.\n" +
                                 "      Do it own your own.\n");
           }
        }
    }

    result 0;
}

local function get_windows_path_env_var_content() : result type string {
    var path type string;
    $path = hefesto.sys.env("WINREG:HKLM\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment\\PATH");
    result $path;
}
